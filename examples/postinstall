#!/bin/zsh --no-rcs

##
## sets the desktop using `desktoppr`
##

# set the path to the desktop image file here
picturepath="/Library/Desktop Pictures/BoringBlueDesktop.png"

# only run when installing on System Volume
if [[ "$3" != "/" ]]; then
    echo "Not installing on /, not setting desktop"
    exit 0
fi

# verify the image exists
if [[ ! -f "$picturepath" ]]; then
    echo "no file at $picturepath, exiting"
    exit 1
fi

# path to current directory of the script
scriptdir=$(dirname "$0")

# try to locate desktoppr
if [[ -x "$scriptdir/desktoppr" ]]; then
    desktoppr="$scriptdir/desktoppr"
elif [[ -x "/usr/local/bin/desktoppr" ]]; then
    desktoppr="/usr/local/bin/desktoppr"
else
    echo "cannot find desktoppr, exiting"
    exit 1
fi

# get the current user
loggedInUser=$(echo "show State:/Users/ConsoleUser" | scutil | awk '/Name :/ { print $3 }')
uid=$(id -u "$loggedInUser")

if [[ "$loggedInUser" != "loginwindow" ]]; then
    # set the desktop for the user
    
    if [[ $(sw_vers -buildVersion) > "21" ]]; then
        # behavior with sudo seems to be broken in Montery
        # dropping the sudo will result in a warning that desktoprr seems to be
        # running as root, but it will still work
        launchctl asuser "$uid" "$desktoppr" "$picturepath"
    else
        sudo -u "$loggedInUser" launchctl asuser "$uid" "$desktoppr" "$picturepath"
    fi
else
    echo "no user logged in, no desktop set"
fi
